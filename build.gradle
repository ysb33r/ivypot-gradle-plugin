//
// ============================================================================
// (C) Copyright Schalk W. Cronje 2013-2015
//
// This software is licensed under the Apache License 2.0
// See http://www.apache.org/licenses/LICENSE-2.0 for license details
//
// Unless required by applicable law or agreed to in writing, software distributed under the License is
// distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and limitations under the License.
//
// ============================================================================
//

// PLEASE NOTE: Integration Tests and Gradle Compatibility tests download a number of files.


plugins {
    id 'groovy'
    id 'maven'
    id 'com.gradle.plugin-publish' version '0.9.9'
    id 'com.github.hierynomus.license' version '0.12.1'
    id 'org.ysb33r.gradletest' version '1.0'
}

apply from: 'gradle/integration-tests.gradle'

version = '0.7-SNAPSHOT'
group = 'org.ysb33r.gradle'
sourceCompatibility = 1.6
targetCompatibility = 1.6

ext {
    groovyLongVer = GroovySystem.version
    groovyShortVer = GroovySystem.version.replaceAll(/\.\d+$/,'')

    ivyJar = fileTree("${gradle.gradleHomeDir}/lib/plugins") {
        include 'ivy*.jar'
    }
}

task foo {
    doLast {
        println ivyJar.files
    }
}
repositories {
    jcenter()
}

dependencies {
    compile 'org.ysb33r.gradle:grolifant:0.5'
    compile gradleApi()
    compile localGroovy()

    testCompile "org.spockframework:spock-core:1.1-groovy-${groovyShortVer}", {
        exclude module : 'groovy-all'
    }
    testCompile 'org.slf4j:slf4j-api:1.7.13'
    testCompile 'org.slf4j:slf4j-simple:1.7.13'

    testRuntime ivyJar
}

test {
    systemProperties DONT_LOOK_FOR_IVY_JAR : 1
}

integrationTest {
    systemProperties TESTROOT : buildDir

    if(gradle.startParameter.isOffline()) {
        systemProperties OFFLINE: 1
    }
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = false
    mapping {
        groovy ='DOUBLESLASH_STYLE'
    }
    ext.year = '2013-2017'
    excludes(['**/*.ad', '**/*.asciidoc', '**/*.adoc', '**/*.md','**/*.properties','**/*CompatibilitySpec.groovy'])
}

pluginBundle {
    description = 'A plugin that keeps a local repository for offline of isolated environments'
    website     = 'https://github.com/ysb33r/ivypot-gradle-plugin'
    vcsUrl      = 'https://github.com/ysb33r/ivypot-gradle-plugin.git'

    tags = ['repositoryManagement','repositories']

    plugins {
        ivypotPlugin {
            id = 'org.ysb33r.ivypot'
            displayName = 'Plugin for maintaining an offline respository'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = 'ivypot'
    }

}

publishPlugins {
    group 'release'
    onlyIf { !version.endsWith("SNAPSHOT") }
    mustRunAfter build
}

gradleTest {
    versions '4.0.2', '4.1', '4.2.1'
    versions '4.3.1', '4.4.1', '4.5'
    versions '4.6', '4.7'

    inputs.files jar
    inputs.dir file('gradleTest')

    beforeTest {
        println "  ${it.name}"
    }
}

task release {
    group 'release'
    description 'Life-cycle task for releasing the plugin'
    dependsOn build, publishPlugins
}
